/* VERSION: 2023-08-23                                               */
/*‚------------------------------------------------------------------*/
/*‚crtbndcl pgm(BERNARD85/xexecppc1)                                 */
/*‚         SRCFILE(BERNARD85/SRC)   strstmf('/?/src/xexecppc1.clle')*/
/*‚         dftactgrp(*no)                                           */
/*‚         actgrp(*new)                                             */
/*‚------------------------------------------------------------------*/
PGM        PARM(&XSCRIPT &FILE &PARMS &callLvl)

DCL        VAR(&XSCRIPT)    TYPE(*CHAR) LEN(10)
DCL        VAR(&FILE)       TYPE(*CHAR) LEN(20)
dcl        VAR(&XSCRIPTFIL) TYPE(*CHAR) LEN(10)
DCL        VAR(&XSCRIPTLIB) TYPE(*CHAR) LEN(10)
DCL        VAR(&callLvl)    TYPE(*DEC) LEN(2)
DCL        VAR(&PARMS)      TYPE(*CHAR) LEN(740) /* 2+(80+2)*9 */
DCL        VAR(&RETCOD)     TYPE(*char) LEN(1)
DCL        VAR(&jobtype)    TYPE(*char) LEN(1)
DCL        VAR(&QDATETIME)  TYPE(*char) LEN(20)
DCL        VAR(&TEXT)       TYPE(*char) LEN(50)
DCL        VAR(&TAG)        TYPE(*char) LEN(4)

DCL        VAR(&SBM)        TYPE(*CHAR) LEN(4) value(x'23E28294')
DCL        VAR(&GO)         TYPE(*CHAR) LEN(3) value(x'238796')
DCL        VAR(&OK)         TYPE(*CHAR) LEN(3) value(x'3B9692')
DCL        VAR(&KO)         TYPE(*CHAR) LEN(3) value(x'2B9296')
/*‚------------------------------------------------------------------*/
/*‚1) Init fields                                                    */
/*‚------------------------------------------------------------------*/
CHGVAR     VAR(&XSCRIPTFIL) VALUE(%SST(&FILE 1 10))
CHGVAR     VAR(&XSCRIPTLIB) VALUE(%SST(&FILE 11 10))
RTVJOBA    TYPE(&JOBTYPE)

/*‚------------------------------------------------------------------*/
/*‚1) monitor the launch                                             */
/*‚------------------------------------------------------------------*/
             IF cond(&CALLLVL = 0) then(DO)
             if cond(&JOBTYPE = '0') then(+
             chgvar var(&TAG) VALUE(&SBM))
             else +
             chgvar var(&TAG) VALUE(&GO)
             callsubr subr(MONITOR)
             ENDDO
/*‚------------------------------------------------------------------*/
/*‚2) on first call check the log file member                        */
/*‚------------------------------------------------------------------*/
IF         COND(&callLvl = 0) THEN(DO)
CHKOBJ     OBJ(&XSCRIPTLIB/XLOG) OBJTYPE(*FILE) MBR(&XSCRIPT)

MONMSG     MSGID(CPF9815) +
  EXEC(ADDPFM FILE(&XSCRIPTLIB/XLOG) MBR(&XSCRIPT))

MONMSG     MSGID(CPF9801) +
  EXEC(CRTSRCPF FILE(&XSCRIPTLIB/XLOG) RCDLEN(228) MBR(&XSCRIPT))

CLRPFM     FILE(&XSCRIPTLIB/XLOG) MBR(&XSCRIPT)
OVRDBF     FILE(XLOG) TOFILE(&XSCRIPTLIB/XLOG) MBR(&XSCRIPT) +
           OVRSCOPE(*JOB)
ENDDO

/*‚------------------------------------------------------------------*/
/*‚3) override the script                                            */
/*‚------------------------------------------------------------------*/
OVRDBF     FILE(XSCRIPT) TOFILE(&XSCRIPTLIB/&XSCRIPTFIL) +
    MBR(&XSCRIPT) OVRSCOPE(*ACTGRPDFN)

/*‚------------------------------------------------------------------*/
/*‚4) call interpreter                                               */
/*‚------------------------------------------------------------------*/
CALL       PGM(XEXECPPC2) +
PARM(&RETCOD &XSCRIPT &XSCRIPTLIB &XSCRIPTFIL &parms &callLvl &jobtype)

/*‚------------------------------------------------------------------*/
/*‚5) free the overrides                                             */
/*‚------------------------------------------------------------------*/
DLTOVR     FILE(XSCRIPT) LVL(*ACTGRPDFN)

IF         COND(&callLvl = 0) THEN(DO)
  DLTOVR FILE(XLOG) LVL(*JOB)
  monmsg msgid(CPF9841)
enddo

RCLACTGRP ACTGRP(*ELIGIBLE)

/*‚------------------------------------------------------------------*/
/*‚6) Message to caller                                              */
/*‚------------------------------------------------------------------*/
         if COND(&CALLLVL = 0) then(DO)
select
when       COND(&RETCOD = '0') then(chgvar var(&tag) value(&ok))
when       COND(&RETCOD = '2') then(chgvar var(&tag) value(&ko))
when       COND(&RETCOD = '1') then(chgvar var(&tag) value(&sbm))
endselect
         callSUBR subr(MONITOR)
         enddo

/*‚------------------------------------------------------------------*/
/*‚7) Message to caller                                              */
/*‚------------------------------------------------------------------*/
select
when cond(&retcod = '0') then(+
sndpgmmsg msg('The script ended normaly') MSGTYPE(*COMP))
when cond(&retcod = '1') then(+
sndpgmmsg msg('The script has been self submited') MSGTYPE(*COMP))
when cond(&retcod = '2') then(+
sndpgmmsg msgid(CPC1219) msgf(QCPFMSG) msgtype(*escape))
endselect

/*‚------------------------------------------------------------------*/
/*‚Monitor the step                                                  */
/*‚------------------------------------------------------------------*/
             SUBR       SUBR(MONITOR)
             RTVMBRD    FILE(&XSCRIPTLIB/&XSCRIPTFIL) MBR(&XSCRIPT) +
                          TEXT(&TEXT)
             RTVSYSVAL  SYSVAL(QDATETIME) RTNVAR(&QDATETIME)
             /*12345678901234567890*/
             /*AAAAMMDDHHmmSSxxxxxx*/
             CHGVAR     VAR(&TEXT) VALUE(%SST(&TEXT 1 33) || &TAG || +
                          %SST(&QDATETIME 3 6) || ' ' || +
                          %SST(&QDATETIME 9 6))
             CHGPFM     FILE(&XSCRIPTLIB/&XSCRIPTFIL) MBR(&XSCRIPT) +
                          TEXT(&TEXT)
             ENDSUBR
             ENDPGM
